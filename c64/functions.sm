class Functions
{
    init(vm)
    {
        me.runtimeError = vm.runtimeError
        me.vm = vm
    }

    strNative(params)
    {
        if type(params[0]) == Type.String
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }
        
        return [true, "%{params[0]}"]
    }

    floor(num) => math.floor(num)   
    log(num) => math.log(num)
    sin(num) => math.sin(num)
    sqr(num) => math.sqrt(num)
    tan(num) => math.tan(num)

    posNative(params)
    {
        con.write("%{string.char(27)}[6n")
        var x = con.getch()
        
        var s = ""
        if (x >= Keys.Zero and x <= Keys.Nine) or x == Keys.Semicolon then s = s + string.char(x)

        while x != Keys.None
        {
            x = con.getch()
            if (x >= Keys.Zero and x <= Keys.Nine) or x == Keys.Semicolon then s = s + string.char(x)
        }
        con.getch()
        const parts = string.split(s, ";")
        return [true, parts[-1]]
    }

    chrNative(params)
    {
        const num = params[0]
        if type(num) != Type.Number 
        {
            me.runtimeError("?TYPE MISMATCH")
            return [false]
        }
        var result = string.char(num)
        if num == 13 then result = "\n"
        if num == 147 then result = "%{string.char(27)}c"
        return [true, result] 
    }

    valNative(params)
    {
        if type(params[0]) != Type.String
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }
        
        return [true, num(params[0])]
    }

    peekNative(params)
    {
        if type(params[0]) != Type.Number
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }
        const index = params[0]
        if index < 0 or index > 65535 
        {
            me.runtimeError("?ILLEGAL QUANTITY ERROR")
            return [false]
        }
        return [true, me.vm.memory[index]]
    }

    spcNative(params)
    {
        if type(params[0]) != Type.Number
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }
        const n = params[0]
        if n < 0 or n > 255 
        {
            me.runtimeError("?ILLEGAL QUANTITY ERROR")
            return [false]
        }
        if n ==0 then return [true, ""]

        const result = string.join([1..n] select x => " ")

        return [true, result]
    }

    tabNative(params)
    {
        if type(params[0]) != Type.Number
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }
        const n = params[0]
        if n < 0 or n > 255 
        {
            me.runtimeError("?ILLEGAL QUANTITY ERROR")
            return [false]
        }
        if n ==0 then return [true, ""]

        const result = string.join([1..n] select x => "\t")

        return [true, result]
    }

    sgnNative(params)
    {
        if type(params[0]) != Type.Number
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }
        var result = 0
        if params[0] < 0 then result = -1
        if params[0] > 0 then result = 1
        return [true, result]
    }

    lenNative(params)
    {
        if type(params[0]) != Type.String
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }
        return [true, len(params[0])]
    } 

    midNative(params)
    {
        if len(params) < 2 or len(params) > 3 
        {
            me.runtimeError("?SYNTAX ERROR")
            return [false]
        }
        if type(params[0]) != Type.String or type(params[1]) != Type.Number 
            or (len(params) == 3 and type(params[2]) != Type.Number)
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }

        var str = params[0]
        var start = params[1] - 1
        var end = 0
        if len(params) == 3 then end = start + params[2]

        if start < 0 or (end < start and len(params) == 3) 
        {
             me.runtimeError("?ILLEGAL QUANTITY ERROR")
            return [false]
        }

        if start >= len(str) then return [true, ""]

        return [true, str[start:end]]
    }

    leftNative(params)
    {
        if len(params) != 2 
        {
            me.runtimeError("?SYNTAX ERROR")
            return [false]
        }

        if type(params[0]) != Type.String
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }

        if type(params[1]) != Type.Number
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }

        const str = params[0]
        const length = params[1]
        if length <=0 then return [true, ""]
        if len(str) < length then return [true, str]
        return [true, str[0:length]]
    }

    rightNative(params)
    {
        if len(params) != 2 
        {
            me.runtimeError("?SYNTAX ERROR")
            return [false]
        }

        if type(params[0]) != Type.String
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }

        if type(params[1]) != Type.Number
        {
            me.runtimeError("?TYPE MISMATCH ERROR")
            return [false]
        }

        const str = params[0]
        const length = params[1]
        if length <=0 then return [true, ""]
        if len(str) < length then return [true, str]
        return [true, str[-length:0]]
    }

    mathFn(num, func)
    {
        if type(num) != Type.Number then
        {
            me.runtimeError("?TYPE MISMATCH")
            return [false]
        }
        return [true, func(num)]
    }

    atnNative(params)
    {
        const num = params[0]
        if type(num) != Type.Number then
        {
            me.runtimeError("?TYPE MISMATCH")
            return [false]
        }
        return [true, math.atan(num)] 
    }

    cosNative(params)
    {
        const num = params[0]
        if type(num) != Type.Number then
        {
            me.runtimeError("?TYPE MISMATCH")
            return [false]
        }
        return [true, math.cos(num)] 
    }

    expNative(params)
    {
        const num = params[0]
        if type(num) != Type.Number then
        {
            me.runtimeError("?TYPE MISMATCH")
            return [false]
        }
        return [true, math.exp(num)] 
    }

    absNative(params)
    {
        const num = params[0]
        if type(num) != Type.Number then
        {
            me.runtimeError("?TYPE MISMATCH")
            return [false]
        }
        if num >= 0 then return [true, num] else return [true, -num]
    }

    ascNative(params)
    {
        const s = params[0]
        if type(s) != Type.String then
        {
            me.runtimeError("?TYPE MISMATCH")
            return [false]
        }
        if s == "" then
        {
            me.runtimeError("?ILLEGAL QUANTITY ERROR")
            return [false]
        }
        return [true, string.ascii(s)]
    }

    usrNative(params)
    {
        if len(params) != 1
        {
            me.runtimeError("?SYNTAX ERROR.")
            return [false]
        }

        var code = ""
        var start = me.vm.memory[785] * 256 + me.vm.memory[786]
        //print "Start: %{start}"
        while me.vm.memory[start]
        {
            code = code + string.char(me.vm.memory[start++])
        }
        const result = me.runbf(code, params[0])

        return [true, result]
    }

    // brainfuck interpreter
    findClosing(code, ip)
    {
        var brace=1
        for i in [ip+1..len(code)-1]
        {
            if code[i] == "[" then brace++
            if code[i] == "]" then brace--
            if brace == 0 then return i
        }
        print "Error: No matching ']' found"
        return len(code)
    }

    findOpening(code, ip)
    {
        var brace=1
        for i in [ip-1..0]
        {
            if code[i] == "]" then brace++
            if code[i] == "[" then brace--
            if brace == 0 then return i
        }
        print "Error: No matching '[' found"
        return len(code)
    }

    runbf(code, input)
    {
        var ip = 0
        var dataPtr = 0
        var data = [0..30000] select x => 0
        var inBytes = []
        var result = nil
        var outString = false

        if type(input) == Type.Number 
        {
            inBytes << input
        }
        else
        {
            for c in input
                inBytes << string.ascii(c)
        }

        var inPtr = 0
        fn getchar()
        {
            if inPtr < len(inBytes)
            {
                data[dataPtr] = inBytes[inPtr++]
            }
        }

        while(ip < len(code))
        {
            const instr = code[ip]
            if instr == "$" and ip == 0 then outString = true
            if instr == ">" then dataPtr++
            if instr == "<" then dataPtr--
            if instr == "+" then data[dataPtr]=data[dataPtr]+1 //data[dataPtr]++
            if instr == "-" then data[dataPtr]=data[dataPtr]-1 //data[dataPtr]--
            if instr == "." then 
            {
                if outString then
                {
                    if result == nil then result = ""
                    if data[dataPtr] >=0 and data[dataPtr] <= 255 then
                        result = result + string.char(data[dataPtr])
                }
                else
                {
                    if result == nil then result = 0
                    result = result + data[dataPtr]
                }
                    
            }
            if instr == "," then getchar()
            if instr == "*" and dataPtr > 0 then data[dataPtr] = data[dataPtr-1] * data[dataPtr] 
            if instr == "/" and dataPtr > 0 then data[dataPtr] = data[dataPtr-1] / data[dataPtr]
            if instr == "[" and data[dataPtr] == 0 
            {
                ip = me.findClosing(code, ip)
            }
            if instr == "]" and data[dataPtr] != 0 
            {
                ip = me.findOpening(code, ip)
            }

            ip++
        }
        return result
    }
    //
}