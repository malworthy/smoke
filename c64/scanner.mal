enum TokenType
{
    None,
    Load,
    Open,
    Close,
    PrintHash,
    InputHash,
    Input,
    Print,
    Goto,
    Gosub,
    Return,
    Read,
    GetHash,
    Get,
    If,
    Then,
    Dim,
    And,
    For,
    Or,
    Not,
    Next,
    Step,
    To,
    Def,
    Fn,
    End,
    NotEqual,
    LessEqual,
    GreaterEqual,
    Less,
    Greater,
    Equals,
    Plus, //10
    Minus,
    Times,
    Divide,
    LeftParan,
    RightParan,
    SemiColon,
    Colon,
    Comma,
    Identifier, 
    String, //20 
    Number,
    Error,
    Data,
    Eof,
}

class Token
{
    init()
    {
        me.line_number = 0
        me.token_type = TokenType.None
        me.lexeme = ""
    }

    toString()
    {
        return "line: %{me.line_number} type:%{TokenType.name(me.token_type)} lexeme:%{me.lexeme}"
    }
}

class Scanner
{
    init(lines)
    {
        me.lines = lines
        me.start = 0
        me.current = 0
        me.line_count = 0
        me.tokens = []
        me.keywords = ["load", "open", "close", "print#", "input#", "input", "print", "goto", "gosub", "return", "read",
            "get#", "get", "if", "then", "dim", "and", "for", "or", "not", "next", "step", "to", "def", "fn", "end",
            "<>", "<=", ">=","<",">", "=","+","-","*","/","(",")",";",":",","]
    }

    scanTokens()
    {
        for line in me.lines
        {
            var t = me.scanLine(string.trim(line))
            me.tokens = me.tokens + t

            me.current = 0
            me.start = 0
        }
        var eofToken = Token()
        eofToken.token_type = TokenType.Eof
        me.tokens << eofToken
        return me.tokens
    }

    getLineNumber(line)
    {
        var lineNum = ""

        for char in line
        {
            if char >= "0" and char <= "9" then 
                lineNum = lineNum + char
            else
                return lineNum

            me.start++
        }

        return lineNum
    }

    skipWhitespace(line)
    {
        while (line[me.current] == " " or line[me.current] == "\t") and me.current < len(line)-1 do
            me.current++
    }

    makeData(line, line_number)
    {
        var count = 1
        var result = Token()
        result.line_number = line_number
        result.token_type = TokenType.Data

        me.skipWhitespace(line)
       
        // in quotes
        if line[me.current] == "\"" then //"
        {
            me.current++
            while !me.isAtEnd(line) and line[me.current] != "\"" do //"
                result.lexeme = result.lexeme + line[me.current++]
            me.current++
        }
        // identifier
        else // not in quotes
        {
            while !me.isAtEnd(line) and line[me.current] != "," and line[me.current] != ":" do
            {
                result.lexeme = result.lexeme + line[me.current]
                me.current++
            }
            //if !me.isAtEnd(line) then current++
        }
        if !me.isAtEnd(line) and line[me.current] != ":" then me.current++

        return result
    }

    makeToken(line, line_number)
    {
        //print "Scanning %{line[me.current]}"
        var count = 1
        var result = Token()
        result.line_number = line_number

        // keywords
        //me.skipWhitespace(line)

        for word in me.keywords
        {       
            if len(word)+me.current <= len(line) 
                and string.upper(line[me.current:me.current+len(word)]) == string.upper(word)  then
            {
                //print me.current
                result.token_type = count
                result.lexeme = word
                result.line_number = line_number
                me.current = me.current + len(word)

                return result
            }
            count++
        }

        

        // Data
        if len(line[me.current:0]) >= 4 and string.upper(line[me.current:me.current+4]) == "DATA"
        {
            me.current = me.current + 4
            var tokens = []
            while !me.isAtEnd(line) and line[me.current] != ":" do
                tokens << me.makeData(line, line_number)

            return tokens
        }

        // numbers
        if me.isNumber(line[me.current]) then
        {
            while !me.isAtEnd(line) and (me.isNumber(line[me.current]) or line[me.current] == ".") do
            {
                //print "number: %{line[me.current]} current: %{me.current} len: %{len(line)}"
                result.token_type = TokenType.Number
                result.lexeme = result.lexeme + line[me.current]
                me.current++
                //print "In Number"
            }
            //result.lexeme = result.lexeme + line[me.current]
        }
        // strings
        else if line[me.current] == "\"" then //"
        {
            result.token_type = TokenType.String
            me.current++
            while !me.isAtEnd(line) and line[me.current] != "\"" do //"
                result.lexeme = result.lexeme + line[me.current++]
            me.current++
        }
        // identifier
        else if me.isAlpha(line[me.current]) then
        {
            result.token_type = TokenType.Identifier
            while !me.isAtEnd(line) and (me.isNumber(line[me.current]) or me.isAlpha(line[me.current]) or line[me.current] == "\%" or line[me.current] == "$" or line[me.current] == "#" )  do
            {
                //print "number: %{line[me.current]} current: %{me.current} len: %{len(line)}"
                
                result.lexeme = result.lexeme + line[me.current]
                me.current++
            }
            //result.lexeme = result.lexeme + line[me.current]
        }
        else
            me.current++

        return result
    }

    isAtEnd(line)
    {
        return !(me.current < len(line))
    }

    isNumber(char) =>  char >= "0" and char <= "9"
    //{
    //    return char >= "0" and char <= "9"
   // }

    isAlpha(c)  =>
        (c >= "a" and c <= "z") or
        (c >= "A" and c <= "Z") 

    removeRem(line)
    {
        //print "line length %{len(line)}"
        if len(line) < 3 then return line
        var quote = false
        for i in [0..len(line)-4]
        {
            if line[i] == "\"" then quote = !quote // "
            if string.upper(line[i:i+3]) == "REM" and !quote
            {
                return line[0:i]
            }
        }
        return line
    }

    scanLine(line)
    {
        // strip rem from line
        line = me.removeRem(line)
        
        var result = []
        var line_number = me.getLineNumber(line)
        me.current = me.start

        while(me.current < len(line)) do
        {
            var token = me.makeToken(line, line_number)
            if type(token) == Type.List then
                result = result + token
            else
                if token.token_type != TokenType.None then result << token
            //print "After token: %{me.current}"
        }
        if len(result) == 0
        {
            const token = Token()
            token.line_number = line_number
            result << token
        }
        return result
    }
    
}

//const program = ["10 print \"hello\" ", "15 x = x + 1 * 2 + (5 - 2) / 7", "20 goto 10"]



